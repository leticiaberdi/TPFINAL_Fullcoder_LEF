// Titulos
Algoritmo recategorizacion_ingresos_brutos
	Escribir 'Sistema de Gestión Recategorización Ingresos Brutos RS-CABA  '
	Escribir 'Bienvenidos/as, este sistema sirve para saber si tenés que recategorizarte en Enero 2024'
	Escribir 'Aplica solamente a aquellos/as contribuyentes que no declaran local de ventas'
	Escribir ' '
	Escribir ' '
	// Definicion de tipo de dato	
	Definir acumuladoVentas Como Entero
	Definir ventasMensualesEstimadas Como Entero
	Definir categoriaVigente Como Cadena
	Definir nuevaCategoria Como Cadena
	Definir ventasEneAbri Como Entero
	Definir acumuladoVentasEneAbri Como Entero
	Definir ventasMayDic Como Entero
	Definir acumuladVentasMayDic Como Entero
	Definir maximoFacturacion Como Entero
	ventasEneAbri <- 0
	ventasMayDic <- 0
	// Los importes mínimos o máximos de facturación son modificados una vez al año por AGIP, entre enero y febrero de cada año"
	// Importe mínimo de facturación según AGIP
	A <- 0
	// Importes que aplican tanto como mínimos o máximos de cada categoría, identificada con la letra mayúscula"
	A1 <- 748390
	B1 <- 1112460
	C1 <- 1557445
	D1 <- 1934280
	E1 <- 2277690
	F1 <- 2847110
	G1 <- 3416530
	// Importe máximo de facturación según AGIP
	H1 <- 4229990
	// Menú Principal			
	Repetir
		Escribir 'Que desea hacer?'
		Escribir '1. Recategorización Ingresos Brutos'
		Escribir '2. Calcular importe estimado mensual a facturar en los próximos cuatro meses (para permanecer en la misma categoría)'
		Escribir '3. Salir'
		Escribir ' '
		Leer menuPrincipal
		Escribir ' '
		// control de opción del menú que elije el usuario	
		Si menuPrincipal<=0 O menuPrincipal>=4 Entonces
			Escribir 'Ha ingresado una opción errónea. Ingréselo nuevamente, gracias'
		FinSi
		// Opción 1: Recategorización Ingresos Brutos
		Si menuPrincipal=1 Entonces
			// Para saber si tiene que recategorizar, se guarda la categoría actual
			// Control por si el usuario ingresa una letra minúscula o de una categoría inexistente
			Escribir 'Ingresar la letra de tu Categoría vigente'
			Leer categoriaVigente
			categoriaVigente <- MAYUSCULAS(categoriaVigente)
			Mientras categoriaVigente>'H' Hacer
				Escribir 'Ingrese la letra ingresada de su Categoría Vigente nuevamente'
				Leer categoriaVigente
				categoriaVigente <- MAYUSCULAS(categoriaVigente)
			FinMientras
			// Ingresos de Ventas de enero a diciembre, dividido a abril para luego poder consultar estimado de facturación
			// Control por si el usuario introduce un dato no númerico
			// Hasta abril	
			acumuladoVentasEneAbri <- 0
			ventasEneAbri <- 0
			Escribir 'Ingresar las ventas de enero a abril'
			Para x<-1 Hasta 4 Hacer
				Leer captura
				resultado1 <- validar1(captura)
				ventasEneAbri <- resultado1
				Si x<4 Entonces
					Escribir 'Ingrese las ventas del mes siguiente'
				FinSi
				acumuladoVentasEneAbri <- acumuladoVentasEneAbri+ventasEneAbri
			FinPara
			// Desde abril a diciembre, y va a servir para el cálculo del estimado de facturación mensual	
			acumuladoVentasMayDic <- 0
			ventasMayDic <- 0
			Escribir 'Ingresar las ventas de mayo a diciembre'
			Para x<-5 Hasta 12 Hacer
				Leer captura
				resultado2 <- validar2(captura)
				ventasMayDic <- resultado2
				Si x<12 Entonces
					Escribir 'Ingrese las ventas del mes siguiente'
				FinSi
				acumuladoVentasMayDic <- acumuladoVentasMayDic+ventasMayDic
			FinPara
			// El acumulado de ventas para poder comparar y ubicar al contribuyente en la categoria de corresponda	
			acumuladoVentas <- acumuladoVentasEneAbri+acumuladoVentasMayDic
			// Dato a imprimir para poder corroborar que la comparativa está categorizando bien	
			Escribir 'El total de ventas es $ ', acumuladoVentas
			// Acumulado comparado con todas las categorías. Acá no utilicé Según, porque hay que ingresar variable númerica
			// Si quería ingresar la relación lógica como opción daba error, y por eso elegí el condicional Si Entonces
			Si acumuladoVentas>=A Y acumuladoVentas<=A1 Entonces
				nuevaCategoria <- 'A'
			SiNo
				Si acumuladoVentas>A1 Y acumuladoVentas<=B1 Entonces
					nuevaCategoria <- 'B'
				SiNo
					Si acumuladoVentas>B1 Y acumuladoVentas<=C1 Entonces
						nuevaCategoria <- 'C'
					SiNo
						Si acumuladoVentas>C1 Y acumuladoVentas<=D1 Entonces
							nuevaCategoria <- 'D'
						SiNo
							Si acumuladoVentas>D1 Y acumuladoVentas<=E1 Entonces
								nuevaCategoria <- 'E'
							SiNo
								Si acumuladoVentas>E1 Y acumuladoVentas<=F1 Entonces
									nuevaCategoria <- 'F'
								SiNo
									Si acumuladoVentas>F1 Y acumuladoVentas<=G1 Entonces
										nuevaCategoria <- 'G'
									SiNo
										Si acumuladoVentas>G1 Y acumuladoVentas<=H1 Entonces
											nuevaCategoria <- 'H'
										SiNo
											// Esta última opción surgió en la prueba, porque podía excederse en las ventas, y queda afuera de este régimen			
											Si acumuladoVentas>H1 Entonces
												Escribir 'Deberá pasar a Régimen General'
											FinSi
										FinSi
									FinSi
								FinSi
							FinSi
						FinSi
					FinSi
				FinSi
			FinSi
			// Comparativa con categoría anterior para determinar si tiene que recategorizarse o no.								
			Si acumuladoVentas<=H1 Entonces
				Si categoriaVigente=nuevaCategoria Entonces
					Escribir 'No corresponde que se recategorice. Continúa en la categoria  ', categoriaVigente
					CATEGORIARECATEGORIZACION <- categoriaVigente
				SiNo
					Escribir 'Corresponde que se recategorice. Su nueva categoria es ', nuevaCategoria
					CATEGORIARECATEGORIZACION <- nuevaCategoria
				FinSi
			FinSi
		FinSi
		Si menuPrincipal=2 Entonces
			// Estimado de facturación mensual. Le resto 2000$ al cálculo para asegurarme que la facturación emitida sea estimada. 
			// ...Previendo que cuando se informa un importe a facturar a un cliente, no lo tome al centavo y corra
			// ...el riesgo de pasarse de categoría
			Escribir 'Ingresar el máximo de facturación informado por AGIP de su categoría a Enero 2024?'
			Leer maximoFacturacion
			Si acumuladoVentasMayDic>0 Entonces
				ventasMensualesEstimadas <- (maximoFacturacion-2000-acumuladoVentasMayDic)/4
			SiNo
				// Esta opción es por si el usuario elige la opción 2 del menú principal directamente.
				Escribir 'Ingrese el total de ventas de Mayo a Diciembre'
				Leer totalVentasMayDic
				ventasMensualesEstimadas <- (maximoFacturacion-2000-totalVentasMayDic)/4
			FinSi
			Escribir 'El importe mensual a facturar estimado para los próximos 4 meses es  $', ventasMensualesEstimadas
		FinSi
		Escribir ' '
		Escribir ' '
		Si menuPrincipal=3 Entonces
			Escribir 'Gracias por utilizar nuestros servicios'
		FinSi
	Hasta Que menuPrincipal=3
	Escribir ' '
	Escribir ' '
FinAlgoritmo

// funciones para verificar el ingreso de datos númericos en las ventas
Función resultado1 <- validar1 (captura)
	Para i<-0 Hasta Longitud(captura) Con Paso 1 Hacer
		l <- SubCadena(captura,i,i)
		Si l>='0' Y l<='9' Entonces
			cad_a_num <- Concatenar(cad_a_num,l)
		SiNo
			hubo_letra <- Verdadero
		FinSi
	FinPara
	num1 <- ConvertirANumero(cad_a_num)
	Si num1>0 Entonces
		resultado1 <- num1
	SiNo
		Escribir 'no ingresó ningún número'
		Leer captura
		hubo_letra <- Falso
		resultado1 <- validar1(captura)
	FinSi
FinFunción

Función resultado2 <- validar2 (captura)
	Para i<-0 Hasta Longitud(captura) Con Paso 1 Hacer
		l <- SubCadena(captura,i,i)
		Si l>='0' Y l<='9' Entonces
			cad_a_num <- Concatenar(cad_a_num,l)
		SiNo
			hubo_letra <- Verdadero
		FinSi
	FinPara
	num1 <- ConvertirANumero(cad_a_num)
	Si num1>0 Entonces
		resultado2 <- num1
	SiNo
		Escribir 'no ingresó ningún número'
		Leer captura
		hubo_letra <- Falso
		resultado2 <- validar2(captura)
	FinSi
FinFunción
